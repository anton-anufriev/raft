group 'org.dev4fx.raft'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: "com.github.hierynomus.license"

sourceCompatibility = 1.8

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

configurations{
    codecGeneration
}

license {
    header rootProject.file('src/main/resources/LICENSE.template')
    strictCheck true
    include "**/*.java"
    ignoreFailures true
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
}

distributions {
    main {
        contents {
            from('src/main/resources/low-latency.properties') {
                into 'lib'
            }
        }
    }
}


mainClassName = "org.dev4fx.raft.perf.RaftPerf"
applicationName = 'raft-perf'
applicationDefaultJvmArgs = ["-Xms512m", "-Xmx1024m"]

task createMediaDriverApp(type: CreateStartScripts) {
    mainClassName = "io.aeron.driver.MediaDriver"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'media-driver'
    defaultJvmOpts = ["-XX:+UnlockDiagnosticVMOptions", "-XX:GuaranteedSafepointInterval=300000", "-XX:BiasedLockingStartupDelay=0"]
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    from(createMediaDriverApp)
    fileMode = 0755
}

tasks.withType(CreateStartScripts) {
    classpath = files('src/main/resources/low-latency.properties') + classpath
}

dependencies {
    compile project(':raft-log')
}
